;;; init-dashboard.el --- -*- lexical-binding: t -*-
;;
;; Filename: init-dashboard.el
;; Description:
;; Author: devbins
;; Maintainer:
;; Copyright (C) 2019 devbins
;; Created: Wed Feb 12 19:25:50 2020 (+0800)
;; Version:
;; Package-Requires: ()
;; Last-Updated:
;;           By:
;;     Update #: 9
;; URL:
;; Doc URL:
;; Keywords:
;; Compatibility:
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;; Commentary:
;;
;;
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;; Change Log:
;;
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; This program is free software: you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation, either version 3 of the License, or (at
;; your option) any later version.
;;
;; This program is distributed in the hope that it will be useful, but
;; WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
;; General Public License for more details.
;;
;; You should have received a copy of the GNU General Public License
;; along with GNU Emacs.  If not, see <https://www.gnu.org/licenses/>.
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;; Code:


;; Dashboard
(use-package dashboard
  :diminish (dashboard-mode page-break-lines-mode)
  :functions (all-the-icons-material
              winner-undo
              widget-forward)
  :custom-face (dashboard-heading ((t (:inherit (font-lock-string-face bold)))))
  :pretty-hydra
  ((:title (pretty-hydra-title "Dashboard" 'material "dashboard" :height 1.1 :v-adjust -0.225)
    :color pink :quit-key "q")
   ("Navigator"
    (("H" browse-homepage "homepage" :exit t)
     ("R" restore-previous-session "recover session" :exit t)
     ("L" restore-session "list sessions" :exit t))
    "Section"
    (("}" dashboard-next-section "next")
     ("{" dashboard-previous-section "previous")
     ("r" dashboard-goto-recent-files "recent files")
     ("m" dashboard-goto-bookmarks "projects")
     ("p" dashboard-goto-projects "bookmarks"))
    "Item"
    (("RET" widget-button-press "open" :exit t)
     ("<tab>" widget-forward "next")
     ("C-i" widget-forward "next")
     ("<backtab>" widget-backward "previous")
     ("C-n" next-line "next line")
     ("C-p" previous-line "previous  line"))
    "Misc"
    (("<f2>" open-dashboard "open" :exit t)
     ("g" dashboard-refresh-buffer "refresh" :exit t)
     ("Q" quit-dashboard "quit" :exit t))))
  :bind (("<f2>" . open-dashboard)
         :map dashboard-mode-map
         ("<mouse-1>" . widget-button-click)
         ("<mouse-2>" . widget-button-click)
         ("H" . browse-homepage)
         ("R" . restore-previous-session)
         ("L" . restore-session)
         ("q" . quit-dashboard)
         ("h" . dashboard-hydra/body)
         ("?" . dashboard-hydra/body))
  :hook (dashboard-mode . (lambda () (setq-local frame-title-format "")))
  :init
  (setq welcomes '("ç«‹å¿—è¦å¦‚å±±ï¼Œè¡Œé“è¦å¦‚æ°´ï¼Œä¸å¦‚å±±ï¼Œä¸èƒ½åšå®šï¼Œä¸å¦‚æ°´ï¼Œä¸èƒ½æ›²è¾¾ã€‚"
                   "äººé™¤äº†æ­»äº¡ï¼Œå…¶ä»–éƒ½æ˜¯æ“¦ä¼¤ã€‚"
                   "ä½ å¯ä»¥æ‰“ç ´å¸¸è¯†ï¼Œä½†ä¸èƒ½æ²¡æœ‰å¸¸è¯†ã€‚"
                   "ä½ æ˜¯åœ¨æ‹œä½›ï¼Œè¿˜æ˜¯åœ¨æ‹œç§æ¬²ï¼Ÿ"
                   "ä½ æ‰€æœ‰çš„è¿·èŒ«ï¼Œä¸è¿‡æ˜¯æ¸…é†’çš„çœ‹ç€è‡ªå·±æ²‰æ²¦ã€‚"
                   "ä½ çŽ°åœ¨çš„ç”Ÿæ´»ä¸æ˜¯ä½ æƒ³è¦çš„ï¼Œä½†ç»å¯¹æ˜¯ä½ è‡ªæ‰¾çš„ã€‚"
                   "ä¸–é“å¦‚å¼ˆæ£‹ï¼Œå˜åŒ–ä¸å®¹è¦†ã€‚"
                   "åˆ«äººå°±æ˜¯æœ€æ•æ„Ÿçš„ä½ ï¼Œé™„æ‰˜åœ¨å¦ä¸€ä¸ªèº¯å£³ä¸Šã€‚"
                   "å¥½åœ¨ï¼Œåˆ«äººå¦‚ä½•åˆ†æžæˆ‘ï¼Œè·Ÿæˆ‘æœ¬èº«æ˜¯ä¸€ç‚¹å…³ç³»ä¹Ÿæ²¡æœ‰çš„ã€‚"
                   "å¥½æœ‹å‹ä¸æ˜¯é€šè¿‡åŠªåŠ›äº‰å–æ¥çš„ï¼Œè€Œæ˜¯åœ¨å„è‡ªçš„é“è·¯ä¸Šå¥”è·‘é‡è§çš„ã€‚"
                   "å²æœˆæ‚ æ‚ ï¼Œè¡°å¾®åªåŠè‚Œè‚¤ï¼›çƒ­å¿±æŠ›å´ï¼Œé¢“åºŸå¿…è‡´çµé­‚ã€‚"
                   "è¿œç¦»é‚£äº›ä¼å›¾è®©ä½ ä¸§å¤±é›„å¿ƒçš„äººå§ï¼Œå°äººç»å¸¸å¦‚æ­¤ï¼Œè€ŒçœŸæ­£çš„ä¼Ÿäººï¼Œä¼šè®©ä½ è§‰å¾—ä½ ä¹Ÿå¯ä»¥å˜å¾—ä¼Ÿå¤§ã€‚"
                   "ä¸è¦å˜²ç¬‘é‚£äº›æ¯”ä½ ä»¬æ‹¼å‘½åŠªåŠ›çš„äººï¼Œä¹Ÿä¸è¦ç†ä¼šé‚£äº›å˜²ç¬‘ä½ æ‹¼å‘½åŠªåŠ›çš„äººã€‚â€”â€”æ¾ä¸‹å¹¸ä¹‹åŠ©"
                   "å²æœˆéª›è¿‡ï¼Œå±±é™µæµ¸è¿œã€‚â€”â€”ã€ŠåŽæ±‰ä¹¦ã€‹"
                   "åªè¦ä½ è§æ€§å¿—è¯šï¼Œå¿µå¿µå›žé¦–å¤„ï¼Œå³æ˜¯çµå±±ã€‚"
                   "å¤§é£Žå¯ä»¥å¹èµ·ä¸€å¼ ç™½çº¸ï¼Œå´æ— æ³•å¹èµ°ä¸€åªè´è¶ï¼Œå› ä¸ºç”Ÿå‘½çš„åŠ›é‡åœ¨äºŽä¸é¡ºä»Žã€‚â€”â€”å†¯éª¥æ‰"
                   "ç¹èŠ±è½å°½ï¼Œæˆ‘å¿ƒä¸­ä»ç•™æœ‰èŠ±è½çš„å£°éŸ³ï¼Œä¸€æœµã€ä¸€æœµï¼Œåœ¨æ— äººçš„å±±é—´è½»è½»é£˜è½ã€‚â€”â€”æ…•å®¹ç†™"
                   "çŸ®äººçœ‹æˆä½•æ›¾è§ï¼Œéƒ½æ˜¯éšäººè¯´é•¿çŸ­ã€‚â€”â€”èµµç¿¼"
                   "å¦‚åŒæ˜Žæœˆå°†æ­»é‚£æ ·ç”Ÿæ´»ï¼Œå¦‚åŒæ°¸è¿œä¸æ­»é‚£æ ·æ±‚çŸ¥ã€‚â€”â€”ç”˜åœ°"))
  (setq dashboard-banner-logo-title (nth (random (length welcomes)) welcomes)
        dashboard-startup-banner 'official
        dashboard-center-content t
        dashboard-show-shortcuts nil
        dashboard-items '((recents  . 10)
                          (bookmarks . 5)
                          (projects . 5))

        dashboard-set-init-info t
        dashboard-set-file-icons (display-graphic-p)
        dashboard-set-heading-icons (display-graphic-p)
        dashboard-heading-icons '((recents   . "file-text")
                                  (bookmarks . "bookmark")
                                  (agenda    . "calendar")
                                  (projects  . "file-directory")
                                  (registers . "database"))

        dashboard-set-footer t
        dashboard-footer (format "Powered by devbins, %s" (format-time-string "%Y"))
        dashboard-footer-icon (cond ((display-graphic-p)
                                     (all-the-icons-faicon "heart"
                                                           :height 1.1
                                                           :v-adjust -0.05
                                                           :face 'error))
                                    ((char-displayable-p ?ðŸ§¡) "ðŸ§¡ ")
                                    (t (propertize ">" 'face 'font-lock-doc-face)))

        dashboard-set-navigator t
        dashboard-navigator-buttons
        `(((,(when (display-graphic-p)
               (all-the-icons-octicon "mark-github" :height 1.1 :v-adjust 0.0))
            "Homepage" "Browse homepage"
            (lambda (&rest _) (message "Home")))
           (,(when (display-graphic-p)
               (all-the-icons-material "restore" :height 1.35 :v-adjust -0.24))
            "Restore" "Restore previous session"
            (lambda (&rest _) (restore-previous-session)))
           (,(when (display-graphic-p)
               (all-the-icons-octicon "tools" :height 1.0 :v-adjust 0.0))
            "Settings" "Open init file"
            (lambda (&rest _) (find-file (expand-file-name "init.el" user-emacs-directory))))
           (,(if (display-graphic-p)
                 (all-the-icons-faicon "question" :height 1.2 :v-adjust -0.1)
               "?")
            "" "Help (?/h)"
            (lambda (&rest _) (dashboard-hydra/body))
            font-lock-string-face))))

  (dashboard-setup-startup-hook)
  :config
  (defun my-banner-path (&rest _)
    "Return the full path to banner."
    (expand-file-name "banner.txt" user-emacs-directory))
  (advice-add #'dashboard-get-banner-path :override #'my-banner-path)

  ;; WORKAROUND: fix differnct background color of the banner image.
  ;; @see https://github.com/emacs-dashboard/emacs-dashboard/issues/203
  (defun my-dashboard-insert-image-banner (banner)
    "Display an image BANNER."
    (when (file-exists-p banner)
      (let* ((title dashboard-banner-logo-title)
             (spec (create-image banner))
             (size (image-size spec))
             (width (car size))
             (left-margin (max 0 (floor (- dashboard-banner-length width) 2))))
        (goto-char (point-min))
        (insert "\n")
        (insert (make-string left-margin ?\ ))
        (insert-image spec)
        (insert "\n\n")
        (when title
          (dashboard-center-line title)
          (insert (format "%s\n\n" (propertize title 'face 'dashboard-banner-logo-title)))))))
  (advice-add #'dashboard-insert-image-banner :override #'my-dashboard-insert-image-banner)

  (defvar dashboard-recover-layout-p nil
    "Wether recovers the layout.")

  (defun open-dashboard ()
    "Open the *dashboard* buffer and jump to the first widget."
    (interactive)
    ;; Check if need to recover layout
    (if (> (length (window-list-1))
           ;; exclude `treemacs' window
           (if (and (fboundp 'treemacs-current-visibility)
                    (eq (treemacs-current-visibility) 'visible))
               2
             1))
        (setq dashboard-recover-layout-p t))

    (delete-other-windows)

    ;; Refresh dashboard buffer
    (if (get-buffer dashboard-buffer-name)
        (kill-buffer dashboard-buffer-name))
    (dashboard-insert-startupify-lists)
    (switch-to-buffer dashboard-buffer-name)

    ;; Jump to the first section
    (goto-char (point-min))
    (dashboard-goto-recent-files))

  (defun quit-dashboard ()
    "Quit dashboard window."
    (interactive)
    (quit-window t)
    (when (and dashboard-recover-layout-p
               (bound-and-true-p winner-mode))
      (winner-undo)
      (setq dashboard-recover-layout-p nil)))

  (defun restore-previous-session ()
    "Restore the previous session."
    (interactive)
    (when (bound-and-true-p persp-mode)
      (restore-session persp-auto-save-fname)))

  (defun restore-session (fname)
    "Restore the specified session."
    (interactive (list (read-file-name "Load perspectives from a file: "
                                       persp-save-dir)))
    (when (bound-and-true-p persp-mode)
      (message "Restoring session...")
      (quit-window t)
      (condition-case-unless-debug err
          (persp-load-state-from-file fname)
        (error "Error: Unable to restore session -- %s" err))
      (message "Done")))

  (defun dashboard-goto-recent-files ()
    "Go to recent files."
    (interactive)
    (funcall (local-key-binding "r")))

  (defun dashboard-goto-projects ()
    "Go to projects."
    (interactive)
    (funcall (local-key-binding "p")))

  (defun dashboard-goto-bookmarks ()
    "Go to bookmarks."
    (interactive)
    (funcall (local-key-binding "m"))))

(provide 'init-dashboard)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; init-dashboard.el ends here
